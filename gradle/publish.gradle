publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'ca.radiant3'
            artifactId 'jsonrpc-java'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }
            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign configurations.archives
}

task preparePublish(dependsOn: signArchives)

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Client and Server library for JSON-RPC in Java'
            name 'JSON-RPC for Java'
            url 'https://github.com/davidmarquis/jsonrpc-java'
            organization {
                name 'Radiant3 inc.'
                url 'http://www.radiant3.ca'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/davidmarquis/jsonrpc-java/issues'
            }
            licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/davidmarquis/jsonrpc-java'
                connection 'scm:git:git://github.com/davidmarquis/jsonrpc-java.git'
                developerConnection 'scm:git:ssh://git@github.com:davidmarquis/jsonrpc-java.git'
            }
            developers {
                developer {
                    name 'David Marquis'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}